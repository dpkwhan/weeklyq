## Answer
The following code snippet is suggested. I intentionally break it into multiple lines to make the explanations easier.

```q
iocClusters:select time,tif,isIOC:`IOC=tif from orders;
iocClusters:update iocGroup:sums 1_(>)prior (0,isIOC) from iocClusters;
iocClusters:update duration:last time-first time by iocGroup from iocClusters where isIOC;
select from iocClusters where duration=max duration
```

Some explanations:

- Line 1 updates the table with a boolean flag to indicate whether an order is an ``IOC`` order.
- Line 2 creates an IOC group for each IOC cluster. The snippet ``1_(>)prior (0,isIOC)`` flags the first IOC order in an IOC cluster. The ``sums`` simply creates an increasing group index for each IOC cluster.
- Line 3 finds the time duration of each IOC clusters.
- Line 4 gives the IOC clusters with maximal duration.
