## Answer
Below is the suggested answer:

```q
/ Find the distinct trading days and sort them in ascending order
tradingDays:`date xasc select distinct date from volume;

/ Find the start date and end date of the 10 trading days around each date
/ Note that the end date is calculated as current date plus 4
/ One implicit assumption is here the tradingDays table is continuous, i.e.
/ no missing trading day in this table.
tradingDays:update startDate:date i-5,endDate:date i+4 from tradingDays;

/ Join the start/end date with the effective date of the stock split so
/ that for each effective date, we know the stop date it need to look
/ backward and forward.
events:events lj `date xkey tradingDays;

/ Join the target data period and effective date with volume data
volume:volume lj `sym xkey select sym,effectiveDate:date,startDate,endDate from events;

/ Keep only volume data within the target data period
volume:select from volume where date within (startDate;endDate);

/ Label each day as before or after the split effective date
volume:update label:?[date<effectiveDate;`Before;`After] from volume;

/ Calculate the average volume by symbol and whether it is before or after the split
select adv:avg dailyVolume by sym,label from volume
```
