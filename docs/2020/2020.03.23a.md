## Answer

### Use each both
Learn more about ``each`` or ``each both`` [here][maps_each].
```q
exec sum in'[exDest;nbbVenues] from childOrders
```

Each applied to a binary value is also called each both and the infix form can be used.

```q
exec sum exDest in' nbbVenues from childOrders
```
### Use ``where`` and ``any``

```q
exec count i from childOrders where any each exDest=nbbVenues
```

### Use list argument
Note that the anonymous function is unary and the parameter is a list.

```q
exec sum {x[0] in x[1]} each flip (exDest;nbbVenues) from childOrders
```

### Use operator apply ``.``
The operator apply ``.`` applies a function to a list and use the individual list elements as the arguments to the function. This is very much similar to ``*`` operator to unpack argument list to function call in [Python][unpack_py] and the spread operator ``...`` in [JavaScript][spread_js].

```q
exec sum ({x in y}.) each flip (exDest;nbbVenues) from childOrders
```

[maps_each]: https://code.kx.com/q/ref/maps/#each
[operator_apply]: https://code.kx.com/q4m3/6_Functions/#682-operator-apply
[unpack_py]: https://docs.python.org/3.9/tutorial/controlflow.html#unpacking-argument-lists
[spread_js]: https://developer.mozilla.org/en-US/docs/Web/JavaScript/Reference/Operators/Spread_syntax
