## Answer

The suggested answer is as follows.

```q
calculateQuoteImbl:{[lob]
  // Calculate the mid-quote price as the average of the best bid and best ask price
  lob:update midPx:0.5*bidPrice1+askPrice1 from lob;

  // Calculate the weighted average of bid size and ask size across all five price levels
  // Weights for each price level is the reciprocal of the distance between the mid-quote 
  // price and the current price level
  avgSize:select sym,time
      ,weightedBidSize:(1%midPx-bidPrice1;1%midPx-bidPrice2;1%midPx-bidPrice3;1%midPx-bidPrice4;1%midPx-bidPrice5) wavg (bidSize1;bidSize2;bidSize3;bidSize4;bidSize5)
      ,weightedAskSize:(1%askPrice1-midPx;1%askPrice2-midPx;1%askPrice3-midPx;1%askPrice4-midPx;1%askPrice5-midPx) wavg (askSize1;askSize2;askSize3;askSize4;askSize5)
    from lob;
  
  // The final quote imbalance is a value within [-1, 1]
  select sym,time,quoteImbl:(weightedBidSize-weightedAskSize)%weightedBidSize+weightedAskSize from avgSize
  };
calculateQuoteImbl[lob]
```
