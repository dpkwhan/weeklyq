## Answer
Below is one suggested implementation to calculate the limit up limit down prices for Tier 1 securities in the opening auction period and the continuous session up to 15:35 in the U.S. equities market.

```q
calculateLULD:{
    / Makes sure input is a list of float
    price:"f"$(),x;

    / Defines percentage bands
    bandPctCutoff:0.75 0.2 0.05;

    / Defines the lower bound price of each price band
    lowerBound:0.15 0w 0w;

    / Finds the percentage band bucket which each price falls into
    / Note that 0.74999 is used, instead of 0.75
    loc:0.749999 3f binr price;

    / Finds the price bands for the given list of prices
    priceBand:lowerBound[loc]&price*bandPctCutoff loc;

    / Applies the price bands to the given list of prices
    flip (price+priceBand;price-priceBand)
  };
```

My friend [Alvi Kabir](https://www.linkedin.com/in/alvikabir/) provides the following creative solution, which uses the fact that the sorted attribute (`s#) when applied to a dictionary makes the dictionary into a step function. See some official document [here](https://code.kx.com/q/kb/temporal-data/#stepped-attribute). I like a lot the way he handles the case when price is between $0.2 and $0.75.

```q
calculateLULD:{
    d:`s#0 .2 .75 3.0001!(.75;0n;.2;.05);
    p:.15^x*d "f"$(),x;
    flip (x+p;x-p)
  };
```
