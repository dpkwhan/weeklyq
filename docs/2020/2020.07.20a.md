## Answer

The suggested answer is as follows.

```q
rollingTimeWindowVwap:{[trades;wl]
  // Sort the input table by sym and time.
  // This will make the window join below much faster.
  t:`sym`time xasc trades;
  
  // Create a list of timestamps from the given trade table
  times:exec time from t;
  
  // Create a time window for the window join.
  // The time window is a two-element list and each element itself is also a list.
  // The first element is the start time and the corresponding item from the second element is the end time.
  w:(neg[wl]+\:times;times);
  
  // Calculate the notional traded and share quantity traded for each rolling window
  t2:select sym,time,notionalTraded:volume*price,sharesTraded:volume from t;
  res:wj[w;`sym`time;t;(t2;(sum;`notionalTraded);(sum;`sharesTraded))];
    
  // Define the rolling vwap as the ratio of notional traded to shares traded
  select sym,time,price,volume,rollingVwap:notionalTraded%sharesTraded from res
  };
rollingTimeWindowVwap[trades;00:05]
```
